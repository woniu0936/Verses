/*
 * Copyright 2022 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.gradle.kotlin.dsl.compileOnly
import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    `kotlin-dsl`
}

group = "com.verses.buildlogic"

// Configure the build-logic plugins to target JDK 17
// This matches the JDK used to build the project, and is not related to what is running on device.
java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}
tasks.withType<KotlinCompile>().configureEach {
    compilerOptions {
        jvmTarget.set(JvmTarget.JVM_17)
    }
}

repositories {
    google()
    mavenCentral()
    gradlePluginPortal()
}

dependencies {
    // 编译时需要认识 Android 的类 (ApplicationExtension 等)
    compileOnly(libs.android.gradlePlugin)
    // 编译时需要认识 Kotlin 的类
    compileOnly(libs.kotlin.gradlePlugin)
    // 必须包含实现库，这样 Kotlin 类里才能 import com.vanniktech...
    implementation(libs.vanniktech.mavenPublish)
    implementation(libs.dokka.gradlePlugin)
}

gradlePlugin {
    plugins {
        register("internalPublish") {
            id = "com.woniu0936.verses.publish"
            implementationClass = "VersesPublishConventionPlugin"
        }
    }
}
